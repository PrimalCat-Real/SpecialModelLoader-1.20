plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.8.0"
    id 'org.jetbrains.changelog' version "2.0.0"
}

if (System.getenv("GITHUB_REF") != null && System.getenv("GITHUB_REF").startsWith("refs/tags/v")) {
    if (System.getenv("GITHUB_REF").substring("refs/tags/v".length()) != project.mod_version)
        throw new IllegalStateException("The version and tag specification are different")
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceSets {
    test {
        resources {
            srcDirs += [
                    'src/test/generated'
            ]
        }
    }
}

loom {
    runs {
        testmodClient {
            client()
            ideConfigGenerated project.rootProject == project
            name = "Testmod Client"
            source sourceSets.test
        }

        testmodDatagen {
            server()

            ideConfigGenerated project.rootProject == project

            name = "Testmod Data"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/test/generated")}"

            runDir "build/datagen"
            source sourceSets.test
        }
    }
}
//test.dependsOn runTestmodDatagen

changelog {
    repositoryUrl = rootProject.repository_url
    introduction = """
    Changelog to track updates for this mod.  
    Add your changes to Unreleased if you want to commit.  
    Please write according to [Keep a Changelog](https://keepachangelog.com/en/1.0.0/)
    """
    combinePreReleases = false
}


repositories {

}

configurations {
    shadowIn
    modImplementation.extendsFrom shadowIn
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    shadowIn 'de.javagl:obj:0.4.0'
}

shadowJar {
    configurations = [project.configurations.shadowIn]
    archiveClassifier.set("dev-shadow")

    relocate 'de.javagl', 'dev.felnull.specialmodelloader.impl.include.de.javagl'
}

remapJar {
    inputFile.set(shadowJar.archiveFile)
    dependsOn shadowJar
    archiveClassifier.set(null)
    setArchivesBaseName("${rootProject.archives_base_name}-mc${rootProject.minecraft_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name
            from components.java
            pom {
                name = 'SpecialModelLoader'
                description = 'Support for loading special models.'
                developers {
                    developer {
                        id = 'MORIMORI0317'
                        name = 'MORIMORI0317'
                    }
                    developer {
                        id = 'FelNull'
                        name = 'TeamFelNull'
                        email = 'teamfelnull@felnull.dev'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "https://maven.felnull.dev/"
            credentials {
                username = "felnull"
                password = "${project.maven_put_pass}" != '' ? "${project.maven_put_pass}" : System.getenv('mavenpassword')
            }
        }
    }
}

curseforge {
    if (System.getenv('curesforgeapikey') != null && "${project.curesforge_id}" != '') {
        apiKey = System.getenv('curesforgeapikey')
        project {
            id = "${rootProject.curesforge_id}"

            changelogType = 'markdown'
            changelog = file('./LATEST_CHANGELOG.md')

            releaseType = "${rootProject.release_type}"
            addGameVersion "${rootProject.minecraft_version}"
            addGameVersion 'Java 17'
            addGameVersion "Fabric"
            addGameVersion "Quilt"

            relations {
                requiredDependency "fabric-api"
            }

            mainArtifact(file("${project.buildDir}/libs/${project.archives_base_name}-mc${project.minecraft_version}-${project.mod_version}.jar")) {
                displayName = "${project.mod_display_name}-${project.minecraft_version}-${project.mod_version}"
            }

            addArtifact(file("${project.buildDir}/libs/${project.archives_base_name}-mc${project.minecraft_version}-${project.mod_version}-sources.jar")) {
                displayName = "${project.mod_display_name}-${project.minecraft_version}-${project.mod_version}-sources"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}


if (System.getenv('modrinthapikey') != null && "${modrinth_id}" != '') {
    modrinth {
        token = System.getenv('modrinthapikey')
        projectId = "${project.modrinth_id}"
        versionName = "${project.mod_display_name}-${project.minecraft_version}-${project.mod_version}"
        versionNumber = "${project.minecraft_version}-${project.mod_version}"
        versionType = "${project.release_type}"
        uploadFile = file("${project.buildDir}/libs/${project.archives_base_name}-mc${project.minecraft_version}-${project.mod_version}.jar")
        gameVersions = List.of(project.minecraft_version)
        loaders = ["fabric", "quilt"]
        dependencies {
            required.project("P7dR8mSH")//Fabric API
        }
        changelog = file('./LATEST_CHANGELOG.md').text
    }
}